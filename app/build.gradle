apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    def versionPropsFile = file('version.properties')

    /*
    Version numbering from: http://andreborud.com/android-studio-automatic-incremental-gradle-versioning/
     */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks || 'generateReleaseBuildConfig' in runTasks) {
            value = 1;
        }

        def versionMajor = 1
        def versionMinor = 5
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def version_Code = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = version_Code.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.elixsr.portforwarder"
            minSdkVersion 16
            targetSdkVersion 25
            versionCode version_Code
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
            multiDexEnabled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    signingConfigs {
        def keystoreFile = rootProject.file("keystore.properties")
        if (keystoreFile.exists()) {
            def p = new Properties()
            p.load(new FileInputStream(keystoreFile))
            release {
                storeFile rootProject.file(p['storeFile'])
                storePassword p['storePassword']
                keyAlias p['keyAlias']
                keyPassword p['keyPassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            def keystoreFile = rootProject.file("keystore.properties")
            if (keystoreFile.exists() && signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
        }
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }
}

def playServicesVersion = '17.0.0'
def firebaseVersion = '17.0.0'

dependencies {
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.elixsr:elixsr-core:0.1.8'
    implementation 'com.github.paolorotolo:appintro:4.0.0'
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    testImplementation 'junit:junit:4.12'
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseVersion"
    implementation "com.google.firebase:firebase-database:$firebaseVersion"
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// 自定义 APK 输出文件名：包名-版本号-release.apk 或 包名-版本号-debug.apk
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def appId = variant.applicationId
        def versionName = variant.versionName.replaceAll(/[()\s]/, '-').replaceAll(/--+/, '-')
        def buildType = variant.buildType.name
        outputFileName = "${appId}-${versionName}-${buildType}.apk"
    }
}